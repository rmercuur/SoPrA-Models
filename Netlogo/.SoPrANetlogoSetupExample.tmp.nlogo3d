extensions [csv table]

globals [
  agent-list

  ;for unit-testing
  test-amount-of-context-elements
  test-filter-on-pc
  test-filter-on-trigger
  test-choose-individualistic
]

turtles-own [name]

;socialPracticeElements
breed [resources resource] ;specification of CE
breed [locations location] ;specification of CE
breed [activities activity]
breed [values value]

activities-own [activity-type enacted]

;activityAssociations
breed [habitual-triggers habitual-trigger]
breed [child-parent-relations child-parent-relation]
breed [related-values related-value]

habitual-triggers-own [strength]
child-parent-relations-own [relation-type]
related-values-own [strength]

;agentAssociations
breed [adhered-values adhered-value]

adhered-values-own [strength]

;beliefs
directed-link-breed [has-habitual-triggers has-habitual-trigger]
directed-link-breed [has-child-of-activities has-child-of-activity]
directed-link-breed [has-related-values has-related-value]
directed-link-breed [has-adhered-values has-adhered-value]

has-habitual-triggers-own [i-belief-strength they-belief-strength]
has-child-of-activities-own [i-belief-strength they-belief-strength]
has-related-values-own [i-belief-strength they-belief-strength]
has-adhered-values-own [i-belief-strength they-belief-strength]

;agents
breed [actors actor]
actors-own [habit-rate attention] ;type-of-choice

;links to activity-assocations
directed-link-breed [habitual-trigger-by habitual-trigger-by-a]
directed-link-breed [habitual-triggers-the habitual-triggers-a]
directed-link-breed [with-children with-child]
directed-link-breed [with-parents with-parent]
directed-link-breed [related-value-with-activities related-value-with-activity]
directed-link-breed [related-value-with-values related-value-with-value]
directed-link-breed [adhered-value-to-values adhered-value-to-value]

;extra-needed-for-setup-example
directed-link-breed [owned-context-elements owned-context-element]
undirected-link-breed [shares-context share-context] ;put some objects in the same context
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;set-up and lay-out;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                                                     ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;General set-up function
to setup
    clear-all
    setup-globals
    setup-family
    setup-habitual-triggers
    setup-related-values
    personalize-agents
    setup-layout
    reset-ticks
end

to setup-globals
  setup-values
  setup-global-locations-resources
end

to setup-values
  let instance-list csv:from-file "Input/Case 2 in Netlogo - Values.csv"
  foreach instance-list
  [x -> crt 1 [set breed values set name item 0 x]]
end

to setup-global-locations-resources ;for now only has locations
  let instance-list csv:from-file "Input/Case 2 in Netlogo - Locations And Resources.csv"

  ;create type of location
  foreach instance-list
  [x -> crt 1 [set breed locations set name item 0 x]] ;x here is a list

  let work-locations (amount-of-turtles / 3)
  ask one-of locations with [name = "Work"][
    foreach range work-locations [x -> hatch 1 [set name (word "Work" x)]]
  ]

  let school-locations (amount-of-turtles / 3)
  ask one-of locations with [name = "School"][
    foreach range school-locations [x -> hatch 1 [set name (word "School" x)]]
  ]
end

to setup-family
  repeat amount-of-families [
    let family []
    set family setup-agents family ;also adds the agent to the list
    setup-family-resources family
  ]
end

;Sets up an agent with an activity tree, personal resources (e.g., bike, rain clothes) and adding it to a family
;
;Args:
; my-family  a list of agents representing the family the agent should be added to
;
;Output:
; a list with the input family and the agent added
to-report setup-agents [my-family]
  create-actors agents-per-family [
    setup-activity-tree
    setup-personal-resources
    set my-family lput self my-family
  ]
  report my-family
end

;Sets Up an actity tree using a list of activities, implementation relations and associating them to each value. setting activity type
to setup-activity-tree ;turtle-context
  ;setup activities and beliefs
  let the-actor self
  let activity-list csv:from-file "Input/Case 2 in Netlogo - Activities.csv"
  foreach activity-list
  [x -> hatch 1 [
    set breed activities set name (word item 0 x )
    ]
  ]

  ;setup implementations
  let implementations-list csv:from-file "Input/Case 2 in Netlogo - Implementation.csv"
  foreach implementations-list
  [x ->
    hatch 1 [
      set breed child-parent-relations
      create-with-child-to one-of turtles with [name = (word item 0 x)]
      create-with-parent-to one-of turtles with [name = (word item 1 x)]
      create-has-child-of-activity-from the-actor
    ]
  ]

  ;set activity-type
  ask activities [
    if (count my-in-with-children = 0) [set activity-type "Top Action"]
    if (count my-in-with-parents = 0) [set activity-type "Action"]
    if (not (activity-type = "Action" or activity-type = "Top Action")) [set activity-type "Abstract Action"]
  ]
end

;Sets up personal resources such as your own bike or rain clothes
to setup-personal-resources ;turtle-context
  let pr-list csv:from-file "Input/Case 2 in Netlogo - Personal Resources.csv"
  foreach pr-list
  [x -> hatch 1 [
    set breed resources set name (word item 0 x who)
    create-owned-context-element-from myself
    ]
  ]
end

;Sets up family resources like home or a shared car
;Also puts everyone in one performance context
to setup-family-resources [my-family] ;observer-context
  ;create a home
  let my-home 0 ;empty instantiation

  create-locations 1 [
    set name (word "Home" who)
    create-owned-context-elements-from turtle-set my-family
    create-shares-context-with turtle-set my-family
    set my-home self
  ]

  ;create other resources
  let family-resource-list csv:from-file "Input/Case 2 in Netlogo - Family Resources.csv"
  foreach family-resource-list
  [x -> crt 1 [
    set breed resources set name (word item 0 x who)
    create-owned-context-elements-from turtle-set my-family
    create-share-context-with my-home
    ]
  ]
end

;Sets up habitual trigger assocations between each activity each agent, location, resource and activity
to setup-habitual-triggers ;oberserver-context
  ask activities [
    let the-activity self
    ask context-elements [
      let the-context-element self
      ask actors [
        let the-agent self
        hatch 1[
          set breed habitual-triggers
          set strength 0.5;
          create-habitual-trigger-by-a-to the-context-element
          create-habitual-triggers-a-to the-activity
          create-has-habitual-trigger-from the-agent
        ]
      ]
    ]
  ]
end

;Sets up habitual trigger assocations between each activity each agent, location, resource and activity
to setup-related-values ;observer-context
  ask activities [
    let the-activity self
    ask values [
      let the-value self
      ask actors [
        let the-agent self
        hatch 1[
          set breed related-values
          set strength 0.5;
          create-related-value-with-value-to the-value
          create-related-value-with-activity-to the-activity
          create-has-related-value-from the-agent
        ]
      ]
    ]
  ]
end

to setup-adhered-values ;observer-context
  ask values [
      let the-value self
      ask actors [
        let the-agent self
        hatch 1[
          set breed adhered-values
          set strength 0.5;
          create-adhered-value-to-value-to the-value
          create-has-adhered-values-from the-agent
        ]
      ]
    ]
end


to personalize-agents
  ask related-values  [set strength random-float 1]

  ask adhered-values [ set strength (max list (min list (random-normal 1 0.25) 1) 0)]

  ask actors [
    set habit-rate (max list (min list (random-normal habit-rate-mu habit-rate-sd ) 1) 0)
  ]
end

;Sets Up the lay-out (shapes, colours and arrangement) one-time
to setup-layout
  ifelse layout
  [
    ;shapes
    ;classes
    set-default-shape activities "arrow"
    set-default-shape values "flag"
    set-default-shape resources"tree"

    ;links
    set-default-shape has-habitual-triggers "bel"
    set-default-shape has-child-of-activities  "bel"
    set-default-shape has-related-values  "bel"
    set-default-shape has-adhered-values  "bel"

    ;colours
    ;classes
    ask activities [set color red]
    ;ask physical-context-elements [set color white]
    ask values [set color blue]
    ask actors [set shape "person"]

    ;links
    ask child-parent-relations [set color red]

    ;hide
    ask beliefs [hide-link]

    ;lay-out
    ifelse three-dimensional
    [repeat 30 [ layout-spring turtles links link-resistance-to-change max-link-length nodes-repel-strength ]]
    [repeat 10 [layout-tutte activities links 12]
      layout-circle activities 6]
  ]
   [
    ask turtles [hide-turtle]
    ask links [hide-link]
  ]
end

;Repeated update for the lay-out (thickness, colour depending on enactment)
to layout-update
  if layout [
    ask activities [
      ifelse enacted
      [set color green
        ask my-links [set color green]]
      [set color red
        ask my-links [set color red]]
    ]
    ;thickness
    ask child-parent-relations [ask my-out-links [set thickness 0.5]]
    ask related-values [let my-strength strength ask my-out-links [set thickness my-strength / 4]]
      ask habitual-triggers [let my-strength strength ask my-out-links [set thickness my-strength / 4]]
        ask adhered-values [let my-strength strength ask my-out-links [set thickness my-strength / 4]]
  ]
end


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; Utility Functions;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;NB: For time-complexity it might be better to store it as a path and retrieve it as an action
;reports lists of activities chosen
to-report my-performance-context
  report other (turtle-set ([share-context-neighbors] of share-context-neighbors) share-context-neighbors) ;self/(located U location)
end

;Reports back the Top Action of the agent
;TODO: generalize if there are multiple representations possible
to-report my-top-action
  report one-of activities with [activity-type = "Top Action"]
end

to-report amount-of-turtles
  report amount-of-families * agents-per-family
end

;reports true if set1 is a subset of set2
to-report is-subset [set1 set2]
  let unaccounted-for set1 with [not member? self set2]
  report not any? unaccounted-for
end

;reports those that are in set1 but not set2 LIST
to-report subtract-left-from-right-list [dead-items-list list1]
  report filter [x -> not member? x dead-items-list] list1
end

to-report subtract-left-from-right-set [set1 set2]
  let unaccounted-for set2 with [not member? self set1]
  report unaccounted-for
end

to-report context-elements
  report (turtle-set actors locations resources activities)
end

to-report beliefs
  report (link-set has-habitual-triggers has-child-of-activities has-related-values has-adhered-values)
end

;;;;;;;;;;;;;;;Testing;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
to unit-test
  check-context-elements
end

to check-context-elements
  set test-amount-of-context-elements
    (count context-elements > 5 and
    count context-elements < 50)
end
@#$#@#$#@
GRAPHICS-WINDOW
0
0
1321
1322
-1
-1
13.0
1
10
1
1
1
0
1
1
1
-50
50
-50
50
-50
50
1
0
1
ticks
30.0

BUTTON
30
30
93
63
NIL
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

SLIDER
23
94
195
127
agents-per-family
agents-per-family
0
5
1.0
1
1
NIL
HORIZONTAL

SWITCH
974
68
1099
101
debug-mode
debug-mode
1
1
-1000

SLIDER
26
629
213
662
link-resistance-to-change
link-resistance-to-change
0
1
0.4
0.1
1
NIL
HORIZONTAL

SLIDER
25
674
197
707
max-link-length
max-link-length
0
100
19.0
1
1
NIL
HORIZONTAL

SLIDER
25
722
197
755
nodes-repel-strength
nodes-repel-strength
0
100
39.0
1
1
NIL
HORIZONTAL

BUTTON
974
25
1080
58
Run Unit Test
unit-test
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

MONITOR
978
111
1174
156
Amount Of Context Element Test
test-amount-of-context-elements
17
1
11

SWITCH
26
587
180
620
three-dimensional
three-dimensional
0
1
-1000

TEXTBOX
27
518
175
536
Layout
11
0.0
1

SLIDER
16
209
188
242
habit-threshold
habit-threshold
0
1
0.29
0.01
1
NIL
HORIZONTAL

SLIDER
23
135
195
168
amount-of-families
amount-of-families
0
5
1.0
1
1
NIL
HORIZONTAL

SLIDER
17
251
189
284
habit-rate-mu
habit-rate-mu
0
1.0
0.01
0.01
1
NIL
HORIZONTAL

SLIDER
19
288
191
321
habit-rate-sd
habit-rate-sd
0
1
0.04
0.01
1
NIL
HORIZONTAL

PLOT
312
65
512
215
habit-strength
tick
strength
0.0
10.0
0.4
0.6
true
true
"" ""
PENS
"mean" 1.0 0 -16777216 true "" "plot mean [strength] of habitual-triggers"
"sd" 1.0 0 -7500403 true "" "plot standard-deviation [strength] of habitual-triggers"

PLOT
557
59
903
288
Histogram strength
strength
turtles
0.0
1.0
0.0
10.0
true
false
"set-plot-pen-mode 1\nset-plot-x-range 0 1\nset-plot-y-range 0 10\nset-histogram-num-bars 10" ""
PENS
"default" 1.0 0 -16777216 true "" "histogram [strength] of habitual-triggers"

SWITCH
1114
68
1225
101
transcribe
transcribe
1
1
-1000

TEXTBOX
226
241
376
297
Standard:\nHabit Threshold: 0.6\nHabit-Rate mu: 0.03\nHabit-Rate sd: 0.5
11
0.0
1

SWITCH
26
542
130
575
layout
layout
1
1
-1000

@#$#@#$#@
## WHAT IS IT?

(a general understanding of what the model is trying to show or explain)

## HOW IT WORKS

(what rules the agents use to create the overall behavior of the model)

## HOW TO USE IT

(how to use the model, including a description of each of the items in the Interface tab)

## THINGS TO NOTICE

(suggested things for the user to notice while running the model)

## THINGS TO TRY

(suggested things for the user to try to do (move sliders, switches, etc.) with the model)

## EXTENDING THE MODEL

(suggested things to add or change in the Code tab to make the model more complicated, detailed, accurate, etc.)

## NETLOGO FEATURES

(interesting or unusual features of NetLogo that the model uses, particularly in the Code tab; or where workarounds were needed for missing features)

## RELATED MODELS

(models in the NetLogo Models Library and elsewhere which are of related interest)

## CREDITS AND REFERENCES

(a reference to the model's URL on the web if it has one, as well as any other necessary credits, citations, and links)
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

sheep
false
15
Circle -1 true true 203 65 88
Circle -1 true true 70 65 162
Circle -1 true true 150 105 120
Polygon -7500403 true false 218 120 240 165 255 165 278 120
Circle -7500403 true false 214 72 67
Rectangle -1 true true 164 223 179 298
Polygon -1 true true 45 285 30 285 30 240 15 195 45 210
Circle -1 true true 3 83 150
Rectangle -1 true true 65 221 80 296
Polygon -1 true true 195 285 210 285 210 240 240 210 195 210
Polygon -7500403 true false 276 85 285 105 302 99 294 83
Polygon -7500403 true false 219 85 210 105 193 99 201 83

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

train passenger car
false
0
Polygon -7500403 true true 15 206 15 150 15 135 30 120 270 120 285 135 285 150 285 206 270 210 30 210
Circle -16777216 true false 240 195 30
Circle -16777216 true false 210 195 30
Circle -16777216 true false 60 195 30
Circle -16777216 true false 30 195 30
Rectangle -16777216 true false 30 140 268 165
Line -7500403 true 60 135 60 165
Line -7500403 true 60 135 60 165
Line -7500403 true 90 135 90 165
Line -7500403 true 120 135 120 165
Line -7500403 true 150 135 150 165
Line -7500403 true 180 135 180 165
Line -7500403 true 210 135 210 165
Line -7500403 true 240 135 240 165
Rectangle -16777216 true false 5 195 19 207
Rectangle -16777216 true false 281 195 295 207
Rectangle -13345367 true false 15 165 285 173
Rectangle -2674135 true false 15 180 285 188

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

wolf
false
0
Polygon -16777216 true false 253 133 245 131 245 133
Polygon -7500403 true true 2 194 13 197 30 191 38 193 38 205 20 226 20 257 27 265 38 266 40 260 31 253 31 230 60 206 68 198 75 209 66 228 65 243 82 261 84 268 100 267 103 261 77 239 79 231 100 207 98 196 119 201 143 202 160 195 166 210 172 213 173 238 167 251 160 248 154 265 169 264 178 247 186 240 198 260 200 271 217 271 219 262 207 258 195 230 192 198 210 184 227 164 242 144 259 145 284 151 277 141 293 140 299 134 297 127 273 119 270 105
Polygon -7500403 true true -1 195 14 180 36 166 40 153 53 140 82 131 134 133 159 126 188 115 227 108 236 102 238 98 268 86 269 92 281 87 269 103 269 113

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 3D 6.0.4
@#$#@#$#@
need-to-manually-make-preview-for-this-model
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180

agent-link
0.0
-0.2 0 0.0 1.0
0.0 1 2.0 2.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180

bel
0.0
-0.2 0 0.0 1.0
0.0 0 0.0 1.0
0.2 0 0.0 1.0
link direction
true
0

imp
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180

sp-link
0.0
-0.2 0 0.0 1.0
0.0 1 4.0 4.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
